I ::= "title:    Progress Reports"
^ "author:   Matteo\n"
^ "language: english\n"
^ "status:   \n"
^ "topic:    \n"
^ "audience: \n"
^ "comment: \n"
^ "created:  9/11/2013"
;

S ::= \[THANKS] \[SLIDEREF.(Q|O|P)] [USELESS] QUESTION;

USELESS ::= (so | also | therefore) ^",";

VERY ::= v^(e^)+ ry ;

THANKS ::= [(thanks | thank you) for the] [VERY] (+impressive | nice | good | interesting | -boring) (presentation |
work | results | experiment) ^"." ;

SLIDEREF ::= (Q:((can | could | would) you) | O:_) (Q: go to | O: go to | P: on)
slide SLIDENUMBER. ^ (Q: "?"\ | O: ", please."\ | P: ",");

SLIDENUMBER ::= DIGITS | SLIDENUMBER.nonzero [^SLIDENUMBER.] ;

DIGITS ::= zero: 0 | nonzero: >(1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9) ;

QUESTION ::= DID;

CORESUBPHRASE ::= (to: ACTION.to | ing: ACTION.ing) (the |
your) OBJECT.;

SUBPHRASE ::=  (to: CORESUBPHRASE.to | ing: CORESUBPHRASE.ing) [[^","] (to: CONNECTOR.to | ing: CONNECTOR.ing) (to: CORESUBPHRASE.to | ing: CORESUBPHRASE.ing)];

DID ::= (do | did | would) you ((VERB SUBPHRASE).(to|ing) | SUBPHRASE.to) ^"?";

VERB ::= to: (+try | suggest | -prefer | claim | offer) to | ing: ([dis^]agree on | recommend | advise | (put | push)
forward);

ACTION ::= (compar (to: ^e | ing: ^ing)) [to] | (chang (to: ^e | ing: ^ing)) |
(improv (to: ^e | ing: ^ing)) | (increas (to: ^e | ing: ^ing)) | (extend
(to: _ | ing: ^ing)) | (speed (to: _ | ing: ^ing) up) | (slow (to: _ | ing:
^ing) down) | (reduc (to: ^e | ing: ^ing)) | (investigat (to: ^e | ing:
^ing)) | (explain (to: _ | ing: ^ing)) | (align (to: _ | ing: ^ing)) |
(measur (to: ^e | ing: ^ing)) | (install (to: _ | ing: ^ing)) |
(correct (to: _ | ing: ^ing)) | (account (to: _ | ing: ^ing) for) |
(imagin (to: ^e | ing: ^ing)) | (support (to: _ | ing: ^ing)) |
(operat (to: ^e | ing: ^ing)) | (decreas (to: ^e | ing: ^ing)) |
(extend (to: _ | ing: ^ing)) | (review (to: _ | ing: ^ing)); 

OBJECT ::= nr: (rotation | x | y | z | zz) axis | data | "p-value"
| plot | sample | grating[^s] | absorption | differential phase | "dark-field"
| noise | accuracy | image quality | slit[^s] | dose | +gold standard |
+ground truth | "contrast-to-noise" ratio | "signal-to-noise ratio" |
>(contrast | noise | phase) transfer function | "Fourier" >(component |
space) | transform | grid | polar reconstruction | ct algorithm |
algorithm[^s] | reconstruction algorithm | local tomography | sinogram[^s] | 
cluster | ++(old | new | python) pipeline | monochromator | source size | silicon 111 |
alignment | area 
[high] throughput | ++>QUANTITY | >(pink | white | monochromatic) beam
spectrum | beam energy | r: >(-speed | range | (end|mid)^point | fit | amount
| importance | relevance | capacity | power spectrum | efficiency | [sheer] size | distance | spectrum) of the OBJECT.nr;

QUANTITY ::= [average] (dose | [exposure] time | throughput | temperature | flux | "SNR" | "CNR" | photons | speed
| "region-of-interest" | "ROI" | pixel size | size | resolution | pixels |
"Talbot" order | "source-to-detector" distance | coherence | bandwidth |
"Fresnel fringes") [per QUANTITY];

CONNECTOR ::= ing: (instead of | rather than ) | [>(and | or)] [maybe]
[also] to: to;
